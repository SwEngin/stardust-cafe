buildscript {
    ext {
        springBootVer = "2.0.5.RELEASE"
        springCloudContractVersion = "2.0.1.RELEASE"
        springCloudContractPactVersion = "1.2.5.RELEASE"
    }

    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "http://repo.spring.io/plugins-release" }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVer"
        classpath "org.springframework.cloud:spring-cloud-contract-gradle-plugin:$springCloudContractVersion"
    }
}

apply plugin: "java"
apply plugin: "idea"
apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"
apply plugin: "spring-cloud-contract"
apply plugin: "maven-publish"

repositories {
    jcenter()
    mavenLocal()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Finchley.RELEASE"
    }
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "net.karneim:pojobuilder:4.2.2"
    compile "org.apache.commons:commons-collections4:4.2"

    testCompile "org.springframework.cloud:spring-cloud-starter-contract-verifier"
    testCompile "org.springframework.boot:spring-boot-starter-test"
}

ext {
    generatedSourcesRoot = "${projectDir}/generated-sources/java"
}

compileJava.options.compilerArgs += ['-s', generatedSourcesRoot]

idea.module {
    generatedSourceDirs += file(generatedSourcesRoot)
    sourceDirs += file(generatedSourcesRoot)
}

group = "com.swengin"
version = "0.1-SNAPSHOT"

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

bootJar {
    baseName = "stardust-cafe"
}

tasks.withType(JavaCompile) {
    options.fork = true
    options.incremental = true
}


tasks.withType(Test) {
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
}

contracts {
    baseClassForTests = "com.swengin.stardust.cafe.StardustCafeTestBase"
}

wrapper {
    gradleVersion = "4.10.2"
    distributionType = Wrapper.DistributionType.ALL
}